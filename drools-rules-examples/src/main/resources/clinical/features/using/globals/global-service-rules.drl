package org.drools.workshop.clinical;

import ca.uhn.fhir.model.dstu2.resource.Condition;
import ca.uhn.fhir.model.dstu2.resource.Patient;
import ca.uhn.fhir.model.dstu2.valueset.AdministrativeGenderEnum;
import ca.uhn.fhir.model.dstu2.valueset.ConditionClinicalStatusCodesEnum;
import ca.uhn.fhir.model.dstu2.valueset.ConditionVerificationStatusEnum;

import org.drools.workshop.clinical.model.AsthmaticPatient;
import org.drools.workshop.misc.services.MyService;

import java.util.List;

global List rulesFired;
global MyService myService;

rule "Identify Diabetic Patients"
    when
        $p: Patient()
        exists (
            Condition(
                patient.resource == $p,
                code.codingFirstRep.system == "http://snomed.info/sct",                 //SNOMED-CT
                code.codingFirstRep.code == "73211009",                                 //Diabetes mellitus (disorder)
                clinicalStatus == ConditionClinicalStatusCodesEnum.ACTIVE.code,
                verificationStatus == ConditionVerificationStatusEnum.CONFIRMED.code
            )
        )
    then
        rulesFired.add(drools.getRule().getName());
end

rule "Identify Asthmatic Patients"
    when
        $p: Patient()
        exists (
            Condition(
                patient.resource == $p,
                code.codingFirstRep.system == "http://snomed.info/sct",                 //SNOMED-CT
                code.codingFirstRep.code == "195967001",                                //Asthma (disorder)
                clinicalStatus == ConditionClinicalStatusCodesEnum.ACTIVE.code,
                verificationStatus == ConditionVerificationStatusEnum.CONFIRMED.code
            )
        )
    then
        myService.notify(drools.getRule().getName());
end